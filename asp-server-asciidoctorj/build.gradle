 /*============================================================================
 * Build file for subproject 'asp-server-asciidoctorj'
 *
 * Root build file: "${rootProject.projectDir}/build.gradle"
 * ============================================================================
 */
dependencies {

    implementation library.asciidoctorj_impl
	
    implementation library.asciidoctorj_pdf
    
    implementation library.asciidoctorj_diagram
    
    api project(':asp-server-core')
    
    // https://mvnrepository.com/artifact/org.mockito/mockito-all
    testImplementation library.mockito_all

}

jar {
    manifest {
        attributes 'AspServer-Version': "${version}"
    }
}

task buildDist(type: Jar) {

    group = 'ASP'
    description = 'build distribution'
    classifier = 'dist'
    
    manifest {
        attributes 'Main-Class': 'de.jcup.asp.server.asciidoctorj.AsciidoctorJServerApplication',
                    'AspServer-Version': "${version}"
    }
    baseName = "${project.name}"
    from { 
         configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } 
    }
    with jar
}


publishing {
    publications {
        bintrayDistPublication(MavenPublication) {
            from components.java
            artifact buildDist
            groupId 'de.jcup.asp'
            artifactId "${project.name}"
            version "${version}"
        }
    }
}

/**
 * Define artifacts
 */
artifacts {
    archives buildDist
}
