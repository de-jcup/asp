dependencies {
    api project(':asp-core')
    api project(':asp-client-java') // having already all dependency necessary for client, we juts integrate the small client parts too
    
    // we export asciidoctorj api here to have it available inside other tools
    api library.asciidoctorj_api
}

task buildDist(type: Jar) {
    group = 'ASP'
    description = 'build distribution'
    classifier = 'dist'

    manifest {
        // we use this to identify version inside lib. so lib can be renamed without versioning 
        attributes 'Launcher-Version': "${version}"
    }
    baseName = "${project.name}"
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        bintrayDistPublication(MavenPublication) {
            from components.java
            artifact buildDist
            groupId 'de.jcup.asp'
            artifactId "${project.name}"
            version "${version}"
        }
    }
}
/**
 * Define artifacts
 */
artifacts {
    archives buildDist
    
}